#!/bin/zsh
#
# The directorys you might want to add to your path, the last one will be the first.

typeset -U path
path_save=(/usr/bin /bin)
# mark the path_save read-only
typeset -r path_save


function zsh_path_prepend() {
	for dir in $*
		test -d $dir && path=($dir $path)
}

function zsh_path_append()  {
	for dir in $*
		test -d $dir && path=($path $dir)
}

function zsh_path_update() {
	
	# cleanup the path
	path=($path_save)
	
	# solaris stuff
	for dir in apache apache2 appserver ccs java dt openwin \
		perl5 proc sadm snadm xpg4 xpg6 sfw
		test -d /usr/$dir && zsh_path_prepend /usr/$dir/sbin /usr/$dir/bin

	# x11 stuff
	zsh_path_prepend /usr/X11R6/bin

	# Games
	zsh_path_prepend /usr/games

	# sbin stuff
	zsh_path_prepend /sbin
	zsh_path_prepend /usr/sbin

	# expand all stuff from /opt
	setopt NULL_GLOB
	for opt in /opt	$HOME/opt
		test -d $opt && for dir in $opt/*
			test -d $dir && zsh_path_prepend $dir/sbin $dir/bin
	unsetopt NULL_GLOB

	zsh_path_prepend /sw/bin
	zsh_path_prepend /usr/pkg/bin
	zsh_path_prepend /usr/local/bin

	# needed for netbsd's root and lazy admins
	zsh_path_prepend /sw/sbin
	zsh_path_prepend /usr/pkg/sbin
	zsh_path_prepend /usr/local/sbin

	zsh_path_prepend $HOME/bin
}

zsh_path_update

