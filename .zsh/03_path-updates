#!/bin/zsh
#
# The directorys you might want to add to your path, the last one will be the first.

typeset -U path
path_safe=(/usr/bin /bin)
# mark the path_safe read-only
typeset -r path_safe

if [ -z "$MANPATH" ]; then
	if [ -x /usr/bin/manpath ]; then
		MANPATH=`/usr/bin/manpath -q`
	else
		MANPATH="/usr/man:/usr/share/man:/usr/X11R6/man:/usr/local/man"
	fi
fi

export MANPATH
typeset -U manpath

export INFOPATH
typeset -U infopath
typeset -T INFOPATH infopath
infopath_safe=(/usr/share/info)
typeset -r infopath_safe

export PERL5LIB
typeset -U perl5lib
typeset -T PERL5LIB perl5lib

function zsh_path_update() {
	
	# cleanup the path
	path=($path_safe)
	infopath=($infopath_safe)
	
	# solaris stuff
	for dir in apache apache2 appserver ccs java dt openwin \
		perl5 proc sadm snadm xpg4 xpg6 sfw; do
		if [ -d /usr/$dir ]; then
			zsh_var_prepend path /usr/$dir/sbin /usr/$dir/bin
			zsh_var_prepend manpath /usr/$dir/share/man /usr/$dir/man
			zsh_var_prepend infopath /usr/$dir/share/info
			zsh_var_prepend perl5lib /usr/$dir/lib/perl5 /usr/$dir/lib/perl5/vendor_perl
		fi
	done

	# x11 stuff
	RDLNK=""
	if which readlink >/dev/null 2>&1; then
		RDLNK=readlink
	elif which greadlink >/dev/null 2>&1; then
		RDLNK=greadlink
	fi

	if [ -z "$RDLNK" ] || [ ! "$($RDLNK /usr/X11R6/bin)" = "../bin" ]; then
		zsh_var_prepend path /usr/X11R6/bin
		zsh_var_prepend manpath /usr/X11R6/man
	fi

	# Games
	zsh_var_prepend path /usr/games

	# sbin stuff
	zsh_var_prepend path /sbin
	zsh_var_prepend path /usr/sbin

	# ccache
	zsh_var_prepend path /usr/lib/ccache

	# expand all stuff from /opt
	setopt NULL_GLOB
	for opt in /opt	$HOME/opt
		test -d $opt && for dir in $opt/*; do
			if [ -d $dir ]; then
				zsh_var_prepend path $dir/sbin $dir/bin
				zsh_var_prepend manpath $dir/share/man $dir/man
				zsh_var_prepend infopath $dir/share/info
				zsh_var_prepend perl5lib $dir/lib/perl5 $dir/lib/perl5/vendor_perl \
					$dir/lib/perl5/darwin
			fi
		done
	# Some fixes for newer Perl under MacPorts.
	zsh_var_append perl5lib /Network/Library/Perl /Library/Perl /System/Library/Perl/Extras \
		/System/Library/Perl
	unsetopt NULL_GLOB

	for dir in /sw /usr/pkg /usr/local; do
		zsh_var_prepend path $dir/bin
		zsh_var_prepend manpath $dir/share/man $dir/man
		zsh_var_prepend infopath $dir/share/info
		zsh_var_prepend perl5lib $dir/lib/perl5 $dir/lib/perl5/vendor_perl \
			$dir/lib/perl5/darwin
	done

	# needed for netbsd's root and lazy admins
	for dir in /sw /usr/pkg /usr/local; do
		zsh_var_prepend path $dir/sbin
	done

	zsh_var_prepend path $HOME/bin
}

zsh_path_update

